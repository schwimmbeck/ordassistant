# -*- version: ord2 -*-
# CMOS inverter (NOT gate) â€” the most basic digital logic gate.
# Uses one NMOS pull-down transistor and one PMOS pull-up transistor.
# When input a=1, NMOS conducts and output y is pulled to vss (logic 0).
# When input a=0, PMOS conducts and output y is pulled to vdd (logic 1).
# Demonstrates: basic cell structure, viewgen symbol/schematic, Nmos/Pmos
# instantiation, pin connections (--), parameter access (.$l, .$w),
# for-loop iteration over instances.
from ordec.core import *
from ordec.schematic import helpers
from ordec.lib.generic_mos import Nmos,Pmos
from ordec.ord2.context import ctx, OrdContext

cell Inv:
    viewgen symbol:
        inout vdd(.align=Orientation.North)
        inout vss(.align=Orientation.South)
        input a(.align=Orientation.West)
        output y(.align=Orientation.East)

    viewgen schematic:
        port vdd(.pos=(2,13); .align=Orientation.North)
        port vss(.pos=(2,1); .align=Orientation.South)
        port y (.pos=(9,7); .align=Orientation.West)
        port a (.pos=(1,7); .align=Orientation.East)

        Nmos pd:
            .s -- vss
            .b -- vss
            .d -- y
            .pos = (3,2)
        Pmos pu:
            .s -- vdd
            .b -- vdd
            .d -- y
            .pos = (3,8)
            .$l = 400n

        pd.$l = 350n

        for instance in pu, pd:
            instance.g -- a
